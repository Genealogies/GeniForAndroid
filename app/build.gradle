apply plugin: 'com.android.application'

def fileChiavi = rootProject.file("chiavi.properties")
def chiave

android {
	if( fileChiavi.exists() ) {
		chiave = new Properties()
		chiave.load( new FileInputStream(fileChiavi) )
		signingConfigs {
			configa {
				keyAlias chiave['keyAlias']
				keyPassword chiave['keyPassword']
				storeFile file(chiave['storeFile'])
				storePassword chiave['storePassword']
			}
		}
	}
	compileSdkVersion 28
	defaultConfig {
		applicationId "app.familygem"
		minSdkVersion 16
		targetSdkVersion 28
		versionCode 7
		versionName '0.7.0'
		vectorDrawables.useSupportLibrary = true
		if (chiave) {
			buildConfigField "String", "utenteAruba", "\""+chiave['utenteAruba']+"\""
			buildConfigField "String", "passwordAruba", "\""+chiave['passwordAruba']+"\""
		}
	}
	buildTypes {
		debug {
			if (chiave)
				signingConfig signingConfigs.configa
		}
		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),'proguard-rules.pro'
			if (chiave)
				signingConfig signingConfigs.configa
		}
	}
	sourceSets {
		main {
			java {
				exclude 'app/familygem/scarti/'
			}
		}
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'org.slf4j:slf4j-api:1.7.28'
	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'
	implementation 'androidx.recyclerview:recyclerview:1.0.0'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
	implementation 'com.google.android.material:material:1.0.0'
	implementation 'com.google.code.gson:gson:2.8.5'
	implementation 'com.google.android.gms:play-services-places:17.0.0'
	implementation 'commons-io:commons-io:2.6'
	implementation 'commons-net:commons-net:3.6'
	implementation 'org.jsoup:jsoup:1.12.1'
	implementation 'com.otaliastudios:zoomlayout:1.3.0'
	implementation 'joda-time:joda-time:2.10.3'
	implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
	implementation 'com.squareup.picasso:picasso:2.71828'
}
